# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Radu Andries <admiral0@tuxfamily.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

_realname=zbar
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.10
pkgrel=9
pkgdesc="Application and library for reading bar codes from various sources"
arch=('any')
url="http://zbar.sourceforge.net/"
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-imagemagick")
source=("http://downloads.sourceforge.net/project/${_realname}/${_realname}/${pkgver}/${_realname}-${pkgver}.tar.bz2"
	"windowsandimagemagick.patch")
md5sums=('0fd61eb590ac1bab62a77913c8b086a5'
         '291225949d396ecf828bab78e4773dc3')
options=('strip' 'staticlibs')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ../windowsandimagemagick.patch
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ./configure --prefix=${MINGW_PREFIX} \
	      --build=${MINGW_CHOST} \
	      --host=${MINGW_CHOST} \
	      --target=${MINGW_CHOST} \
	      --without-qt \
	      --without-gtk \
	      --without-python \
	      --disable-video

  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}
